= Perl 6 Introduzione
:descrizione: Introduzione generale a Perl 6
:Author: Naoum Hankache; MarsMarsico <marsmarsico@tiscali.it>
:keywords: perl6, perl 6, introduzione, perl6intro, perl 6 introduzione, perl 6 tutorial, perl 6 intro
:Revision: 1.0
:icons: font
:source-highlighter: pygments
//:pygments-style: manni
:source-language: perl6
:pygments-linenums-mode: table
:toc: left
:doctype: book
:lang: it


Scopo di questo documento è dare una veloce panoramica del linguaggio di 
programmazione Perl 6. Per i neofiti di Perl 6, questo dovrebbe mettere in grado di partire.

Alcune sezioni di questo documento si riferiscono ad altre parti della http://docs.perl6.org[Perl 6 documentation]
(più completa ed accurata); consultale in caso di necessità di maggiori informazioni. 

In questo documento, troverai esempi sulle materie più in voga.
Per comprenderli meglio, prenditi il tempo di riprodurre tutti gli esempi.

.Licenza
Questo lavoro è licenziato sotto Creative Commons Attribution-ShareAlike 4.0 International License.
Per vederne una copia, vista

* https://creativecommons.org/licenses/by-sa/4.0/.

.Contributi
Se vuoi contribuire a questo documento, riferisciti a:

* https://github.com/hankache/perl6intro

.Feedback
tutti i feedback sono benvenuti:
naoum@hankache.com
marsmarsico@tiscali.it

Se questo lavoro ti piace, clicca la _Star_ nel repository che trovi qui:
link:https://github.com/hankache/perl6intro[Github].

.Versioni disponibili
* Bulgaro: http://bg.perl6intro.com
* Cinese: http://zh.perl6intro.com
* Olandese: http://nl.perl6intro.com
* Francese: http://fr.perl6intro.com
* Tedesco: http://de.perl6intro.com
* Giapponese: http://ja.perl6intro.com
* Portoghese: http://pt.perl6intro.com
* Spagnolo: http://es.perl6intro.com

:sectnums:
== Introduzione
=== Cos'è Perl 6
Perl 6 è un linguaggio gradualmente tipizzato, di alto livello, general-purpose.
Perl 6 è multiparadigmatico. Supporta sia programmazione procedurale
che orientatata agli oggetti che funzionale.

.Il motto del Perl :
* TMTOWTDI (Pronounced Tim Toady): "There is more than one way to do it", ossia "c'è più di un modo per farlo".
* Le cose facili debbono restare facili, le difficili più semplici e le impossibili difficili.

=== Gergo
* *Perl 6*: è la specifica di un linguaggio con una test suite.
Le implementazioni che passano la test suite di specifica sono considerate Perl 6.
* *Rakudo*: è un compilatore Perl 6.
* *Rakudobrew*: è un manager d'installazione per Rakudo.
* *Zef*: è un installatore di moduli per Perl 6.
* *Rakudo Star*: è una raccolta che include Rakudo, Zef, una collezione di moduli Perl 6 e della documentazione.

=== Come installare Perl 6
.Linux

Per installare Rakudo Star, lancia questi comandi dal tuo terminale:
----
wget https://rakudo.perl6.org/downloads/star/rakudo-star-2017.04.tar.gz
tar xfz rakudo-star-2017.04.tar.gz
cd rakudo-star-2017.04
perl Configure.pl --gen-moar --prefix /opt/rakudo-star-2017.04
make install
----
Per altre opzioni, vai a http://rakudo.org/how-to-get-rakudo/#Installing-Rakudo-Star-Linux

.macOS
Ci sono quattro opzioni:

* Seguire gli stessi passi indicati per Linux
* Installazione secondo homebrew: `brew install rakudo-star`
* Installazione con MacPorts: `sudo port install rakudo`
* scaricare l'ultimo installer (file con estensione .dmg) from https://rakudo.perl6.org/downloads/star/

.Windows
. Scaricare l'ultimo installer (file con estensione .msi) da https://rakudo.perl6.org/downloads/star/ +
Se la tua architettura è una 32-bit, scarica il file x86; se è una 64-bit, il file x86_64.
. Dopo l'installazione accertati che `C:\rakudo\bin` sia nel PATH

.Docker
. Prendi l'immagine ufficiale per Docker `docker pull rakudo-star`
. Poi fai girare un container con l'immagine `docker run -it rakudo-star`

=== Eseguire codice Perl 6

Eseguire codice Perl 6 può essere fatto usando REPL (Read-Eval-Print Loop).
Per farlo apri un terminale, digita `perl6` e premi [Enter].  
Questo farà apparire il prompt `>`.
Poi digita una linea di codice e digita [Enter].
REPL fornirà il valore della linea. Puoi digitare un'altra linea, oppure `exit`
e premere [Enter] per uscire da REPL.

Alternativamente, scrivi il tuo codice in un file, salvalo e fallo girare.
Si raccomanda che gli script Perl 6 abbiano l'estension `.pl6`.
Fai girare il file digitando `perl6 nomefile.pl6` nel terminale e premi [Enter].
A differenza di REPL, questo non scriverà automaticamente il risultato di
ogni linea: il codice deve contenere una istruzione opportuna (per esempio `say`)
per scrivere un output sul monitor.

REPL in generale si usa per fare delle prove su pezzetti specifici di codice,
tipicamente singole linee. Per programmi con più di una singola linea si raccomanda
di immagazzinarel prima in un file e poi far girare quello.

Linee singole possono essere provate anche non interattivamente sulla linea di comando
attaverso il comando `perl6 -e 'tuo-codice-qui'` e premendo [Enter].

[NOTA]
--
Rakudo Star contiene un editor di liea che permette di fare quasi tutto senza REPL. 

Se hai installato il semplice Rakudo invece di Rakudo Star allor probabilmente non avrai
abilitate le funzionalità di editing  di linea (che sono frecce alto e basso per la storia,
sinistra e destra per editare l'input, completamento con il TAB).
Valuta di far girare i seguenti comandi per avere tutto disponibile:

* `zef install Linenoise` funziona su Windows, Linux e macOS

* `zef install Readline` se sei su Linux e preferisci la libreria _Readline_ library
--

=== Editor
Poiché scriveremo e salveremo il nostro codice Perl 6 per la maggior parte del tempo,
dovremmo avere un buon editor di testo che riconosca la sintassi Perl 6.

Personalmente uso e raccomando https://atom.io/[Atom]. 
Si tratta di un moderno editor di testo che fornisce nativamente riconoscimento sintattico di Perl 6

https://atom.io/packages/language-perl6[Perl 6 FE] è un altro riconoscitore sintattico di Perl 6 per Atom;
è derivato da un package originale corretto e riveduto.

Altre persone della comunità usano anche http://www.vim.org/[Vim], https://www.gnu.org/software/emacs/[Emacs] oppure 
http://padre.perlide.org/[Padre].

Versioni recenti di Vim forniscono il riconoscimento sintattico nativamente.
Emacs e Padre richiederanno invece l'installaizone di package addizionali.

=== Salve Mondo!
Inizieremo dal tradizionale 'salve mondo'

[source,perl6]
say 'salve mondo';

che può anche essere scritto così:

[source,perl6]
'salve mondo'.say;

=== Panoramica sulla sintassi
Perl 6 è *free form*: Sei libero (la maggior parte delle volte) di usare un numero di spazi a piacere.

*Istruzioni* sono tipicamente una linea logica di codice, necessitano di finire con punto e virgola:
`say "Ciao" if True;`

*Espressioni* sono speciali tipi di istruzioni che ritornano un valore:
`1+2` restituirà `3`

Le espressioni sono composte da *Operandi* ed *Operatori*

Gli *operandi* possono essere:

* *Variabili*: un valore che può essere manipolato o cambiato.

* *Costanti*: un valore costante come un numero o una stringa.

Gli *operatore* vengono classificati nei seguenti tipi:

|===

| *Tipo* | *Descrizione* | *Esempio*

| Prefisso | precede l'operando | `++1`

| Infisso | tra operandi | `1+2`

| Postfisso | segue l'operando | `1++`

| Circumfisso | circonda l'operando | `(1)`

| Postcircumfisso | segue un operando, abbraccia l'altro  | `Array[1]`

|===

==== Identificatori
Gli identificatori sono nomi dati agli opernadi quando questi vengono definiti.

.Regole:
* Debbono iniziare con un carattere alfanumerico oppure con un underscore.

* Possono contenere cifre numeriche (eccetto che al primo posto).

* Possono contenere trattini oppure apostrofi (eccetto che al primo o all'ultimo posto), dato che ci deve essere un carattere alfabetico alla destra di ogni trattino o di ogni apostrofo.

|===

| *Corretto* | *Scorretto*

| `var1` | `1var`

| `var-uno` | `var-1`

| `var'uno` | `var'1`

| `var1_` | `var1'`

| `_var` | `-var`

|===

.Notazini convenzionali:
* notazione a cammello: `variabileNo1`

* notazione kebab: `variabile-no1`

* notazione a serpente: `variabile_no1`

Sei libero di nominare i tuoi identificatori come preferisci; è buona pratica adottare una certa notazione in modo uniforme.

Usare nomi significativi facilita la tua vita di programmatore (e quella degli altri!).

* `var1 = var2 * var3` è una istruzione sintatticamente corretta, ma il suo scopo non è di immediata comprensione.
* `salario-mensile = paga-giornaliera * giorni-lavorati` sarebbe un modo milgiore di nominare queste variabili.

==== Commenti
Un commento è un testo che viene ignorato dal compilatore e viene usato come un appunto.

I commenti si dividono in tre tipi:

* su singola linea:
+
[source,perl6]
# questo è un commento su singola linea

* Annidati:
+
[source,perl6]
say #`(questo è un commento annidato) "Salve Mondo."

* Multi-linea:
+
[source,perl6]
-----------------------------
=begin commento
Questo è un commento multilinea.
Commento 1
Commento 2
=end commento
-----------------------------

==== Virgolettato
Le stringhe hanno bisogno di essere delimitate o da doppie virgolette oppure da virgolette singole.

Usa sempre le doppie virgolette se:

* la tua stringa contiene un apostrofo.

* la tua stringa contiene una variabile che deve essere interpolata.

[source,perl6]
-----------------------------------
say 'Salve Mondo';   # Salve Mondo
say "Salve Mondo";   # Salve Mondo
say "l'amo";         # l'amo
my $name = 'John Doe';
say 'Salve $name';   # Salve $name
say "Salve $name";   # Salve John Doe
-----------------------------------

== Operatori

=== Operatori comuni
Nella tabella qui sotto vediamo gli operatori più comunemente usati.
[cols="^.^5m,^.^5m,.^20,.^20m,.^20m", options="header"]
|===

| Operatore | Tipo | Descrizione | Esempio | Risultato

| + | Infisso | Addizione | 1 + 2 | 3

| - | Infisso | Sottrazione | 3 - 1 | 2

| * | Infisso | Moltiplicazione | 3 * 2 | 6

| ** | Infisso | Elevamento a potenza | 3 ** 2 | 9

| / | Infisso | Divisione | 3 / 2 | 1.5

| div | Infisso | Divisione Intera (arrotondamento per difetto) | 3 div 2 | 1

| % | Infisso | Modulo | 7 % 4 | 3

.2+| %% .2+| Infisso .2+| Divisibilità | 6 %% 4 | False

<| 6 %% 3 <| True

| gcd | Infisso | Massimo comune divisore | 6 gcd 9 | 3

| lcm | Infisso | Minimo comune multiplo | 6 lcm 9 | 18

| == | Infisso | Uguaglianza numerica | 9 == 7  | False

| != | Infisso | Disuguaglianza numerica | 9 != 7  | True

| < | Infisso | Minore di | 9 < 7  | False

| > | Infisso | Maggiore di | 9 > 7  | True

| \<= | Infisso | Minore o uguale | 7 \<= 7  | True

| >= | Infisso | Maggiore o uguale | 9 >= 7  | True

| eq | Infisso | Uguaglianza tra stringhe | "Giovanni" eq "Giovanni"  | True

| ne | Infisso | Disuguaglianza tra stringhe | "Giovanni" ne "Giovanna"  | True

| = | Infisso | Assegnamento | my $var = 7  | Assegna il valore di `7` alla variabile `$var`

.2+| ~ .2+| Infisso .2+| Concatenazione tra stringhe | 9 ~ 7 | 97

<m| "Hei " ~ "là"  <| Hei là

.2+| x .2+| Infisso .2+| Replicazione di stringhe | 13 x 3  | 131313

<| "Ciao " x 3  <| Ciao Ciao Ciao

.5+| ~~ .5+| Infisso .5+| Corrispondenza intelligente | 2 ~~ 2  | True

<| 2 ~~ Int <| True

<| "Perl 6" ~~ "Perl 6" <| True

<| "Perl 6" ~~ Str <| True

<| "illuminismo" ~~ /lumi/ <| ｢lumi｣

.2+| ++ | Prefisso | Incremento | my $var = 2; ++$var;  | PRIMA incrementa di 1 la variabile e ritorna il risultato `3`

<m| Postfisso <d| Incremento <m| my $var = 2; $var++;  <| Ritorna la variabile `2` e POI la incrementa

.2+|\--| Prefisso | Decremento | my $var = 2; --$var;  | PRIMA decrementa la variabile di 1 e ritorna il risultato `1`

<m| Postfisso <d| Decremento <m| my $var = 2; $var--;  <| Ritorna la variabile `2` e POI la decrementa

.3+| + .3+| Prefisso .3+| Forza l'operando ad un valore numerico | +"3"  | 3

<| +True <| 1

<| +False <| 0

.3+| - .3+| Prefisso .3+| Forza l'opernaod ad un valroe numerico e ne ritorna la negazione | -"3"  | -3

<| -True <| -1

<| -False <| 0

.6+| ? .6+| Prefisso .6+| Forza l'operando ad un valore booleano | ?0 | False

<| ?9.8 <| True

<| ?"Ciao" <| True

<| ?"" <| False

<| my $var; ?$var; <| False

<| my $var = 7; ?$var; <| True

| ! | Prefisso | Forza l'operando ad un valore booleano e ne ritorna la negazione | !4 | False

| .. | Infisso | Intervallo |  0..5  | Crea un intervallo da 0 a 5

| ..^ | Infisso | Intervallo con estremo escluso |  0..^5  | Crea un intervallo da 0 a 4

| ^.. | Infisso | Intervallo con estremo escluso |  0^..5  | Crea un intervallo da 1 a 5

| \^..^ | Infisso | Intervallo con estremi esclusi |  0\^..^5  | Crea un intervallo da 1 a 4

| ^ | Prefisso | Intervallo unario |  ^5  | Come fosse 0..^5; crea un intervallo da 0 a 4

| ... | Infisso | Costruttore pigro di liste |  0...9999  |  ritorna gli elementi solo se richiesti

.2+| {vbar} .2+| Prefisso .2+| Spalmatore | {vbar}(0..5)  | (0 1 2 3 4 5)

<| {vbar}(0\^..^5)  <| (1 2 3 4)

|===

=== Operatori inversi

Aggiungendo una `R` prima di ogni operatore si rovesceranno i suoi operandi.

[cols=".^m,.^m,.^m,.^m", options="header"]
|===
| Operazione normale | Risultato | Operazione inversa | Risultato

| 2 / 3 | 0.666667 | 2 R/ 3 | 1.5

| 2 - 1 | 1 | 2 R- 1 | -1

|===

=== Operatori di riduzione

Gli operatori di riduzione lavorano su liste di valori.
Si formano avvolgendo l'operatore tra parentesi quadre `[]`

[cols=".^m,.^m,.^m,.^m", options="header"]
|===
| Operazione normale | Risultato | Operatore di riduzione | Risultato

| 1 + 2 + 3 + 4 + 5 | 15 | [+] 1,2,3,4,5 | 15

| 1 * 2 * 3 * 4 * 5 | 120 | [*] 1,2,3,4,5 | 120

|===

NOTA: Per vedere una lista completa degli operatori, incluse le precedenze tra di loro, vai qui https://docs.perl6.org/language/operators

== Variabili
Le variabili Perl 6 sono classificate in tre categorie: scalari, array e hash.

Un *sigillo* è un carattere che viene usato come prefisso per categorizzare le variabili.

* `$` per gli scalari
* `@` per gli array
* `%` per gli hash

=== Scalari
Gli scalari portano un singolo valore oppure un puntatore.

[source,perl6]
----
# Stringa
my $name = 'John Doe';
say $name;

# Interi
my $anni = 99;
say $anni;
----

Sugli scalari si può applicare un gruppo specifico di operazioni, dipendentemente dal valore che portano.

[source,perl6]
.Stringhe
----
my $name = 'John Doe';
say $name.uc;
say $name.chars;
say $name.flip;
----

----
JOHN DOE
8
eoD nhoJ
----

NOTA: Per avere una lista completa dei metodi applicabili alle stringhe, vedi qui https://docs.perl6.org/type/Str

[source,perl6]
.Interi
----
my $anni = 17;
say $anni.is-prime;
----

----
True
----

NOTA: Per avere una lista completa dei metodi applicabili agli interi vei qui https://docs.perl6.org/type/Int

[source,perl6]
.Numeri razionali
----
my $age = 2.3;
say $age.numerator;
say $age.denominator;
say $age.nude;
----

----
23
10
(23 10)
----

NOTA: Per una lista completa dei metodi applicabili ai numeri razionali vedi qui https://docs.perl6.org/type/Rat

=== Array
Gli array (alias vettori, liste etc ndt.) sono liste contenenti valori multipli.

[source,perl6]
----
my @animali = 'cammello','lama','gufo';
say @animali;
----

Sugli array si possono fare un sacco di perazioni, come mostrato nell'esempio sottostante:

Noticina: La tilde `~` si usa per la concatenazione delle stringhe.

[source,perl6]
.`Script`
----
my @animali = 'cammello','vigogna','lama';
say "Nello zoo ci sono " ~ @animali.elems ~ " animali";
say "Gli animali sono: " ~ @animali;
say "Adotterò un gufo per lo zoo";
@animali.push("gufo");
say "Ora il mio zoo ha: " ~ @animali;
say "Il primo animale adottato fu " ~ @animali[0];
@animali.pop;
say "Sfortunatamente il gufo è volato via e siamo rimasti con: " ~ @animali;
say "Stiamo chiudendo lo zoo tenendo un solo animale";
say "Stiamo lasciando andare: " ~ @animali.splice(1,2) ~ " e teniamo il " ~ @animali;
----

.`Output`
----
Nello zoo ci sono 3 animali
Gli animali sono: cammello vigogna lama
Adotterò un gufo per lo zoo
Ora il mio zoo ha: cammello vigogna lama gufo
Il primo animale adottato fu il cammello
Sfortunatamente il gufo è volato via e siamo rimasti con: cammello vigogna lama
Stiamo chiudendo lo zoo tenendo un solo animale
Stiamo lasciando andare: vigogna lama e teniamo il cammello 
----

.Spiegazione
`.elems` ritorna il numero degli elementi di un array. +
`.push()` aggiunge uno o più elementi all'array. +
Possiamo accedere ad uno specifico elemento dell'array specificando la sua posizione `@animali[0]`. +
`.pop` rimuove l'ultimo elemento dell'array e lo restituisce. +
`.splice(a,n)` rimuove `n` elmenti iniziando dalla posizione `a`.

==== array a dimensione fissata
Un array semplice viene dichiarato così:
[source,perl6]
my @array;

L'array semplice può avere una lunghezza qualsiasi e per questo viene chiamato auto-estensibile. +
L'array accetta qualsiasi numero di valori, senza restrizioni.

Al contrario, possiamo creare array a dimesione fissata. +
Questi array non possono ricevere accessi oltre la dimensione data.

Per dichiarare un array di dimensione fissata devi specificare il massimo numero di elementi tra parentesi quadre subito dopo il suo nome:
[source,perl6]
my @array[3];

Questo array potrà tenere al massimo tre valori, indicizzati da 0 a 2.

[source,perl6]
----
my @array[3];
@array[0] = "primo valore";
@array[1] = "secondo valore";
@array[2] = "terzo valore";
----

Non potrai aggiungere un quarto valore:
[source,perl6]
----
my @array[3];
@array[0] = "primo valore";
@array[1] = "secondo valore";
@array[2] = "terzo valore";
@array[3] = "quarto valore";
----


Otterresti il seguente messaggio di errore: 
[source,perl6]
----
Index 3 for dimension 1 out of range (must be 0..2) 
----

==== Array multidimensionali
Gli array che abbiamo visto sono monodimensionali. +
Fortunatamente possiamo definirne anche di multidimensionali in Perl 6.

[source,perl6]
my @tbl[3;2];

Questo array è mulitidimensionale.
La prima dimensione può avere un massimo di tre valori e la seconda un massimo di due.

Pensa ad una griglia 3x2.

[source,perl6]
----
my @tbl[3;2];
@tbl[0;0] = 1;
@tbl[0;1] = "x";
@tbl[1;0] = 2;
@tbl[1;1] = "y";
@tbl[2;0] = 3;
@tbl[2;1] = "z";
say @tbl
----

----
[[1 x] [2 y] [3 z]]
----

.Rappresentazione visuale di un array:
----
[1 x]
[2 y]
[3 z]
----

NOTA: Per avere una guida completa sugli array vedi qui https://docs.perl6.org/type/Array

=== Hash
[source,perl6]
.Un hash è un insime di coppie chiave/valore.
----
my %capitali = ('Italia','Roma','Germania','Berlino');
say %capitali;
----

[source,perl6]
.Un altra forma, più succinta, di popolare un hash:
----
my %capitali = (Italia => 'Roma', Germania => 'Berlino');
say %capitali;
----

Alcuni metodi che possono essere applicati agli hash:
[source,perl6]
.`Script`
----
my %capitali = (Italia => 'Roma', Germania => 'Berlino');
%capitali.push: (Francia => 'Parigi');
say %capitali.kv;
say %capitali.keys;
say %capitali.values;
say "La capitale della Francia è: " ~ %capitali<Francia>;
----

.`Output`
----
(Francia Parigi Germania Berlino Italia Roma)
(Francia Germania Italia)
(Parigi Berlino Roma)
La capitale della Francia è: Parigi
----

.Spiegazione
`.push: (chiave => 'valore')` aggiunge una nuova coppia chiave/valore. +
`.kv` ritorna una lista contenente tutte le chiavi ed i valori. +
`.keys` ritorna una lista contenente tutte le chiavi. +
`.values` ritorna una lista contenente tutti i valori. +
Possiamo accedere ad uno specifico valore nell'hash specificando la sua chaive, così `%hash<key>`

NOTA: Per avere una guida completa sugli hash vedi qui https://docs.perl6.org/type/Hash

=== Tipi
Nei precedenti esempi non abbiamo specificato che tipo di valori le variabili dovrebbero contenere.

Noticina: `.WHAT` ritornerà il tipo di valore contentuo in una variabile.

[source,perl6]
----
my $var = 'Text';
say $var;
say $var.WHAT;

$var = 123;
say $var;
say $var.WHAT;
----

Come puoi vedere nell'esempio qui sopra, il tipo di valore in `$var` prima era (Str) e poi è diventato (Int).

Questo stile di programmazionie viene chiamato tipizzazione dinamica. Dinamica nel senso che le variabili possono contenere valori di ogni tipo (tipo Any ndt).

Prova a far girare questo esempio: +
Nota `Int` prima del nome della variabile.

[source,perl6]
----
my Int $var = 'Text';
say $var;
say $var.WHAT;
----

Fallirà con questo messaggio di errore: `Type check failed in assignment to $var; expected Int but got Str` 
 controllo sul tipo fallito per $var; mi aspettavo un Int ma ho ricevuto Str]

Ciò che è successo è che abbiamo specificato che la variabile avrebbe dovuto essere di un tipo (Int), ma quando l'abbiamo assegnata ad una stringa è fallita.

Questo stile di programmazione si chiama tipizzazione statica. Statica nel senso che i tipi delle variablili sono definiti prima degli assegnamenti e non possono più cambiare.

Il Perl 6 è classificato come *gradualmente tipizzato*; esso permette sia la tipizzazione *statica* che quella *dinamica*.

.Anche array ed hash possono essere tipizzati staticamente:
[source,perl6]
----
my Int @array = 1,2,3;
say @array;
say @array.WHAT;

my Str @multilingual = "Ciao","Salut","Hallo","您好","안녕하세요","こんにちは";
say @multilingual;
say @multilingual.WHAT;

my Str %capitali = (Italia => 'Roma', Germania => 'Berlino');
say %capitali;
say %capitali.WHAT;

my Int %country-codes = (Italia => 39, Germania => 49);
say %country-codes;
say %country-codes.WHAT;
----

.Qui sotto una lista dei tipi più comunemente usati:
Probabilmente non userai mai i primi due ma essi vengono inclusi in questa lista per completezza.

[cols="^.^1m,.^3m,.^2m,.^1m, options="header"]
|===

| *Tipo* | *Descrizione* | *Esempio* | *Risultato*

| Mu | La radice della gerarchia dei tipi del Perl 6 | |

| Any | Classe base per nuove classi e per la maggior parte delle classi predefinite | |

| Cool | Valore che può essere trattato indifferentemente come una stringa oppure come un numero | my Cool $var = 31; say $var.flip; say $var * 2; | 13 62

| Str | Stringa di caratteri | my Str $var = "NEON"; say $var.flip; | NOEN

| Int | Intero (precisioine arbitraria) | 7 + 7 | 14

| Rat | Numero razionale (precisione limitata) | 0.1 + 0.2 | 0.3

| Bool | Booleano | !True | False

|===

=== Introspezione

Introspezione è il processo per cui si ottiene una informazione sulle proprietà di un oggetto, come il tipo. +
In un esempio precedente abbiamo usato `.WHAT` per ottenere il tipo di una variabile.

[source,perl6]
----
my Int $var;
say $var.WHAT;    # (Int)
my $var2;
say $var2.WHAT;   # (Any)
$var2 = 1;
say $var2.WHAT;   # (Int)
$var2 = "Hello";
say $var2.WHAT;   # (Str)
$var2 = True;
say $var2.WHAT;   # (Bool)
$var2 = Nil;
say $var2.WHAT;   # (Any)
----

Il tipo di una variabile che contiene un valore è correlato a tale valore. +
Il tipo di una variabile vuota dichiarata staticamente è il tipo con il quale è stata dichiarata. +
Il tipo di una variabile vuote che non è stata dichiarata staticamente è `(Any)` +
Per cancellare il valroe di una variabile assegnale `Nil`.

=== Ambito
Prima di usare una variabile per la prima volta essa deve essere dichiarata.

Several declarators are used in Perl 6. We've been using `my`, so far.

[source,perl6]
my $var=1;

The `my` declarator give the variable *lexical* scope.
In other words, the variable will only be accessible in the same block it was declared.

A block in Perl 6 is delimited by `{ }`.
If no block is found, the variable will be available in the whole Perl 6 script.

[source,perl6]
----
{
  my Str $var = 'Text';
  say $var;   # is accessible
}
say $var;   # is not accessible, returns an error
----

Since a variable is only accessible in the block where it is defined, the same variable name can be used in another block.

[source,perl6]
----
{
  my Str $var = 'Text';
  say $var;
}
my Int $var = 123;
say $var;
----

=== Assegnamenti e vincoli
Abbiamo visto negli esempi precedenti come *assegnare valori alle variabili. +
Un *assegnamento* viene fatto tramite l'operatore `=`.
[source,perl6]
----
my Int $var = 123;
say $var;
----

IL valore assegnato ad una variabile si può cambiare:

[source,perl6]
.Assegnamento
----
my Int $var = 123;
say $var;
$var = 999;
say $var;
----

.`Output`
----
123
999
----

D'altro canto, non possiamo cambiare il valore di una variabile quando questo è *vincolato*. +
Il *vincolo* viene definito tramite l'operatore `:=`.

[source,perl6]
.Vincolo
----
my Int $var := 123;
say $var;
$var = 999;
say $var;
----

.`Output`
----
123
Cannot assign to an immutable value
----

[source,perl6]
.le variabili possono anche essere vincolate ad altre variabli:
----
my $a;
my $b;
$b := $a;
$a = 7;
say $b;
$b = 8;
say $a;
----

.`Output`
----
7
8
----

Vincolare le variabili è bidirezionale. +
`$a := $b` and `$b := $a` hanno lo stesso effetto.

NOTA: Per avere più informazioni sulle variabli vedi qui https://docs.perl6.org/language/variables

== Funzioni e modificatori

Ci sono differenze tra funzioni e modificatori. +
Le funzioni non cambiano lo stato dell'oggetto su cui vengono chiamate ad operare. +
I modificatori sì.

[source,perl6,linenums]
.`Script`
----
my @numeri = [7,2,4,9,11,3];

@numeri.push(99);
say @numeri;      #1

say @numeri.sort; #2
say @numeri;      #3

@numeri.=sort;
say @numeri;      #4
----

.`Output`
----
[7 2 4 9 11 3 99] #1
(2 3 4 7 9 11 99) #2
[7 2 4 9 11 3 99] #3
[2 3 4 7 9 11 99] #4
----

.Spiegazione
`.push` è un modificatore; esso cambia lo stato dell'array (#1)

`.sort` è una funzione; essa ritorna un array ordinato ma non modifica lo stato iniziale dell'array:

* (#2) mostra che viene ritornato un array ordinato.

* (#3) mostra che l'array iniziale è rimasto immutato.

Per forzare una funzione a comportarsi da modificatore usiamo `.=` invece di `.` (#4) (linea 9 dello script)

== Cicli e condizioni
Perl 6 ha molti costrutti condizionali e ciclici.

=== if
Il codice gira solo se la condizione specificata viene soddisfatta, ossia una espressione viene valutata come `True`.

[source,perl6]
----
my $anni = 19;

if $anni > 18 {
  say 'Benvenuto'
}
----

In Perl 6, possiamo invertire il codice condizionato e la condizione. +
Anche se il codice e la condizione vengono invertiti, la condizione viene sempre valutata per prima.

[source,perl6]
----
my $anni = 19;

say 'Benvenuto' if $anni > 18;
----

Se la condizione non viene soddisfatta, possiamo specificare blocchi da eseguire in alternativa usando:

* `else`
* `elsif`

[source,perl6]
----
# fai girare lo stesso codice per valori dfferenti della variabile
my $numero_di_posti = 9;

if $numero_di_posti <= 5 {
  say 'sono una berlina'
} elsif $numero_di_posti <= 7 {
  say 'sono una station'
} else {
  say 'sono un furgone'
}
----

=== unless
La versione negata di una istruzione `if` può essere `unless`.

Il codice seguente:

[source,perl6]
----
my $scarpe-pulite = False;

if not $scarpe-pulite {
  say 'Pulisciti le scarpe'
}
----
può essere scritto così:

[source,perl6]
----
my $scarpe-pulite = False;

unless $scarpe-pulite {
  say 'Pulisciti le scarpe'
}
----

La negazione, in Perl 6, viene fatta usando o `!` o `not`.

`unless (codizione)` viene usato al posto di `if not (condizione)`.

`unless` non può avere una clausola di alternativa `else`.

=== with

`with` si comporta come `if`, ma controlla se la variabile sia definita.

[source,perl6]
----
my Int $var=1;

with $var {
  say 'Ciao'
}
----

Se fai girare il codice senza assegnare una valore alla variabile, non accade nulla.
[source,perl6]
----
my Int $var;

with $var {
  say 'Ciao'
}
----

`without` è la versione negata di `with`. Puoi vederne un'analogia di `unless`.

se il primo `with` non viene soddisfatto, si può specificare un percorso alternativo con `orwith`. +
`with` ed `orwith` sono analoghi ad `if` ed `elsif`.

=== for

The `for` loop iterates over multiple values.

[source,perl6]
----
my @array = [1,2,3];

for @array -> $array-item {
  say $array-item * 100
}
----

Notice that we created an iteration variable `$array-item` and then performed the operation `*100` on each array item.

=== given

`given` is the Perl 6 equivalent of the switch statement in other languages,
but much more powerful.

[source,perl6]
----
my $var = 42;

given $var {
    when 0..50 { say 'Less than or equal to 50'}
    when Int { say "is an Int" }
    when 42  { say 42 }
    default  { say "huh?" }
}
----

After a successful match, the matching process will stop.

Alternatively `proceed` will instruct Perl 6 to continue matching even after a successful match.
[source,perl6]
----
my $var = 42;

given $var {
    when 0..50 { say 'Less than or equal to 50';proceed}
    when Int { say "is an Int";proceed}
    when 42  { say 42 }
    default  { say "huh?" }
}
----

=== loop

`loop` is another way of writing a `for` loop.

Actually, `loop` is how `for` loops are  written in C-family programming languages.

Perl 6 belongs to the C-family languages.

[source,perl6]
----
loop (my $i = 0; $i < 5; $i++) {
  say "The current number is $i"
}
----

NOTA: For more info on loops and conditions, see https://docs.perl6.org/language/control

== I/O
In Perl 6, two of the most common _Input/Output_ interfaces are the _Terminal_ and _Files_.

=== Basic I/O using the Terminal

==== say
`say` writes to the standard output. It appends a newline at the end. In other words, the following code:

[source,perl6]
----
say 'Hello Mam.';
say 'Hello Sir.';
----
will be written on 2 separate lines.

==== print
`print` on the other hand behaves like `say` but doesn't add a new line.

Try replacing `say` with `print` and compare the results.

==== get
`get` is used to capture input from the terminal.

[source,perl6]
----
my $name;

say "Hi, what's your name?";
$name = get;

say "Dear $name welcome to Perl 6";
----

When the above code runs, the terminal will be waiting for you to input your name. Enter it and then hit [Enter].
Subsequently, it will greet you.

==== prompt
`prompt` is a combination of `print` and `get`.

The above example can be written like this:

[source,perl6]
----
my $name = prompt "Hi, what's your name? ";

say "Dear $name welcome to Perl 6";
----

=== Running Shell Commands
Two subroutines can be used to run shell commands:

* `run` Runs an external command without involving a shell

* `shell` Runs a command through the system shell. It is platform and shell dependent.
All shell meta characters are interpreted by the shell, including pipes, redirects, environment variable substitutions and so on.

[source,perl6]
.Run this if you're on Linux/macOS
----
my $name = 'Neo';
run 'echo', "hello $name";
shell "ls";
----

[source,perl6]
.Run this if you're on Windows
----
shell "dir";
----
`echo` and `ls` are common shell keywords on Linux: +
`echo` prints text to the terminal (the equivalent of `print` in Perl 6) +
`ls` lists all files and folders in the current directory

`dir` is the equivalent of `ls` on Windows.


=== File I/O
==== slurp
`slurp` is used to read data from a file.

Create a text file with the following content:

.datafile.txt
----
John 9
Johnnie 7
Jane 8
Joanna 7
----
[source,perl6]
----
my $data = slurp "datafile.txt";
say $data;
----

==== spurt
`spurt` is used to write data to a file.

[source,perl6]
----
my $newdata = "New scores:
Paul 10
Paulie 9
Paulo 11";

spurt "newdatafile.txt", $newdata;
----

After running the above code, a new file named _newdatafile.txt_ will be created. It will contain the new scores.

=== Working with files and directories
Perl 6 can list the contents of a directory without resorting to shell commands (by using `ls`, for example).

[source,perl6]
----
say dir;                # List files and folders in the current directory
say dir "/Documents";   # List files and folders in the specified directory
----

In addition, you can create and delete directories.

[source,perl6]
----
mkdir "newfolder";
rmdir "newfolder";
----

`mkdir` creates a new directory. +
`rmdir` deletes an empty directory and returns an error if not empty.

You can also check if a path exists; if it is a file; or a directory:

In the directory where you will be running the below script, create an empty folder `folder123` and an empty pl6 file `script123.pl6`

[source,perl6]
----
say "script123.pl6".IO.e;
say "folder123".IO.e;

say "script123.pl6".IO.d;
say "folder123".IO.d;

say "script123.pl6".IO.f;
say "folder123".IO.f;
----

`IO.e` checks if the directory/file exists. +
`IO.f` checks if the path is a file. +
`IO.d` checks if the path is a directory.

WARNING: Windows users can use `/` or `\\` to define directories +
`C:\\rakudo\\bin` +
`C:/rakudo/bin` +

NOTA: For more info on I/O, see https://docs.perl6.org/type/IO

== Subroutines
=== Definition
*Subroutines* (also called *subs* or *functions*) are a means of packaging and reusing functionality. +

A subroutine definition begins with the keyword `sub`. After their definition, they can be called by their handle. +
Check out the below example:

[source,perl6]
----
sub alien-greeting {
  say "Hello earthlings";
}

alien-greeting;
----

The previous example showcased a subroutine that doesn't require any input.

=== Signature
Subroutines can require input. That input is provided by *arguments*.
A subroutine may define zero or more *parameters*.
The number and type of parameters that a subroutine defines is called its *signature*.

The below subroutine accepts a string argument.

[source,perl6]
----
sub say-hello (Str $name) {
    say "Hello " ~ $name ~ "!!!!"
}
say-hello "Paul";
say-hello "Paula";
----

=== Multiple dispatch
It is possible to define multiple subroutines that have the same name but different signatures.
When the subroutine is called, the runtime environment will decide which version to use based on the number and type of supplied arguments. This type of subroutine is defined the same way as normal subs except that we use the `multi` keyword instead of `sub`.

[source,perl6]
----
multi greet($name) {
    say "Good morning $name";
}
multi greet($name, $title) {
    say "Good morning $title $name";
}

greet "Johnnie";
greet "Laura","Mrs.";
----

=== Default and Optional Parameters
If a subroutine is defined to accept an argument, and we call it without providing it with the required argument, it will fail.

Perl 6 provides us the ability to define subroutines with:

* Optional Parameters
* Default Parameters

Optional parameters are defined by appending `?` to the parameter name.

[source,perl6]
----
sub say-hello($name?) {
  with $name { say "Hello " ~ $name }
  else { say "Hello Human" }
}
say-hello;
say-hello("Laura");
----

If the user doesn't need to supply an argument, a default value can be defined. +
This is done by assigning a value to the parameter within the subroutine definition.

[source,perl6]
----
sub say-hello($name="Matt") {
  say "Hello " ~ $name;
}
say-hello;
say-hello("Laura");
----

=== Returning values
All the subroutines we've seen so far *do something* -- they display some text on the terminal.

Sometimes, though, we execute a subroutine for its *return* value so we can use it later in the flow of our program.

If a function is allowed to run through it's block to the end, the last statement or expression will determine the return value.

[source,perl6]
.Implicit return
----
sub squared ($x) {
  $x ** 2;
}
say "7 squared is equal to " ~ squared(7);
----

For the sake of clarity, it might be a good idea to _explicitly_ specify what we want returned.
This can be done using the `return` keyword.
[source,perl6]
.Explicit return
----
sub squared ($x) {
  return $x ** 2;
}
say "7 squared is equal to " ~ squared(7);
----
==== Restricting return values
In one of the previous examples, we saw how we can restrict the accepted argument to be of a certain type.
The same can be done with return values.

To restrict the return value to a certain type, we either use the `returns` trait or the arrow notation `-\->` in the signature.

[source,perl6]
.Using the returns trait
----
sub squared ($x) returns Int {
  return $x ** 2;
}
say "1.2 squared is equal to " ~ squared(1.2);
----

[source,perl6]
.Using the arrow
----
sub squared ($x --> Int) {
  return $x ** 2;
}
say "1.2 squared is equal to " ~ squared(1.2);
----
If we fail to provide a return value that matches the type constraint, an error will be thrown.

----
Type check failed for return value; expected Int but got Rat (1.44)
----

[Noticina]
====
Not only can type constraints control the type of the return value; they can also control its definedness.

In the previous examples, we specified that the return value should be an `Int`.

We could also have specified that the returned `Int` should be strictly defined or undefined using the following signatures: +
`--> Int:D` and `--> Int:U`

That being said, it is good practice to use those type constraints. +
Below is the modified version of the previous example that uses `:D` to force the returned `Int` to be defined.

[source,perl6]
----
sub squared ($x --> Int:D) {
  return $x ** 2;
}
say "1.2 squared is equal to " ~ squared(1.2);
----
====

NOTA: For more info on subroutines and functions, see https://docs.perl6.org/language/functions

== Functional Programming
In this chapter we will take a look at some of the features that facilitate Functional Programming.

=== Functions are first-class citizens
Functions/subroutines are first-class citizens:

* They can be passed as arguments

* They can be returned from other functions

* They can be assigned to variables

A great example is the `map` function. +
`map` is a _higher order function_, it can accept another function as an argument.

[source,perl6]
.Script
----
my @array = <1 2 3 4 5>;
sub squared($x) {
  $x ** 2
}
say map(&squared,@array);
----

.Output
----
(1 4 9 16 25)
----

.Explanation
We defined a subroutine called `squared` that takes an argument and multiplies that argument by itself. +
Next, we used `map`, a higher order function, and gave it two arguments, the `squared` subroutine and an array. +
The result is a list of the squared elements of the array.

Notice that when passing a subroutine as an argument, we need to prepend `&` to its name.

=== Anonymous functions
An *anonymous function* is also called a *lambda*. +
An anonymous function is not bound to an identifier (it has no name).

Let's rewrite the `map` example and have it use an anonymous function
[source,perl6]
----
my @array = <1 2 3 4 5>;
say map(-> $x {$x ** 2},@array);
----
Notice that instead of declaring the squared subroutine and passing it as an argument to `map`, we defined
it within the anonymous subroutine as `\-> $x {$x ** 2}`.

In Perl 6 parlance, we call this notation a *pointy block*

[source,perl6]
.A pointy block may also be used to assign functions to variables:
----
my $squared = -> $x {
  $x ** 2
}
say $squared(9);
----

=== Chaining
In Perl 6, methods can be chained, so you're not required to pass the result of one method to another as an argument.

To illustrate: Given an array, you may need to return the unique values of the array, sorted from biggest to smallest.

Here's a non-chained solution:

[source,perl6]
----
my @array = <7 8 9 0 1 2 4 3 5 6 7 8 9>;
my @final-array = reverse(sort(unique(@array)));
say @final-array;
----
Here, we call `unique` on `@array`, pass the result as an argument to `sort`, and then pass that result to `reverse`.

In contrast, with chained methods, the above example can be rewritten as:

[source,perl6]
----
my @array = <7 8 9 0 1 2 4 3 5 6 7 8 9>;
my @final-array = @array.unique.sort.reverse;
say @final-array;
----

You can already see that chaining methods is _easier on the eye_.

=== Feed Operator
The *feed operator*, called _pipe_ in some functional programming languages, further illustrates
method chaining.

[source,perl6]
.Forward Feed
----
my @array = <7 8 9 0 1 2 4 3 5 6 7 8 9>;
@array ==> unique()
       ==> sort()
       ==> reverse()
       ==> my @final-array;
say @final-array;
----

.Explanation
----
Start with `@array` then return a list of unique elements
                    then sort it
                    then reverse it
                    then store the result in @final-array
----
Note that the flow of the method calls is top-down -- from first to final step.


[source,perl6]
.Backward Feed
----
my @array = <7 8 9 0 1 2 4 3 5 6 7 8 9>;
my @final-array-v2 <== reverse()
                   <== sort()
                   <== unique()
                   <== @array;
say @final-array-v2;
----

.Explanation
The backward feed is like the forward feed, but in reverse. +
The flow of the method calls is bottom-up -- from final to first step.

=== Hyper operator
The *hyper operator* `>>.` will call a method on all elements of a list and return a list of the results.
[source,perl6]
----
my @array = <0 1 2 3 4 5 6 7 8 9 10>;
sub is-even($var) { $var %% 2 };

say @array>>.is-prime;
say @array>>.&is-even;
----

Using the hyper operator we can call methods already defined in Perl 6, e.g. `is-prime` that tells us if a number is prime or not. +
In addition we can define new subroutines and call them using the hyper operator. In this case we have to prepend `&` to the name of the method; e.g., `&is-even`.

This is very practical as it relieves us from writing a `for` loop to iterate over each value.

WARNING: Perl 6 guarantees that the order of the results is the same as that of the original list.
However, there is *no guarantee* that Perl 6 will actually call the methods in list order or in the same thread. So, be careful with methods that have side-effects, such as `say` or `print`.

=== Junctions
A *junction* is a logical superposition of values.

In the below example `1|2|3` is a junction.
[source,perl6]
----
my $var = 2;
if $var == 1|2|3 {
  say "The variable is 1 or 2 or 3"
}
----
The use of junctions usually triggers *autothreading*;
the operation is carried out for each junction element, and all the results are combined into a new junction and returned.

=== Lazy Lists
A *lazy list* is a list that is lazily evaluated. +
Lazy evaluation delays the evaluation of an expression until required, and avoids repeating evaluations by storing results in a lookup table.

The benefits include:

* Performance increase by avoiding needless calculations

* The ability to construct potentially infinite data structures

* The ability to define control flow

To build a lazy list we use the infix operator `...` +
A lazy list has *initial element(s)*, a *generator* and an *endpoint*.

[source,perl6]
.Simple lazy list
----
my $lazylist = (1 ... 10);
say $lazylist;
----
The initial element is 1 and the endpoint is 10. No generator was defined so the default generator is the successor (+1) +
In other words this lazy list may return (if requested) the following elements (1, 2, 3, 4, 5, 6, 7, 8, 9, 10)

[source,perl6]
.Infinite lazy list
----
my $lazylist = (1 ... Inf);
say $lazylist;
----
This list may return (if requested) any integer between 1 and infinity, in other words any integer number.

[source,perl6]
.Lazy list built using a deduced generator
----
my $lazylist = (0,2 ... 10);
say $lazylist;
----
The initial elements are 0 and 2 and the endpoint is 10.
No generator was defined, but using the initial elements, Perl 6 will deduce that the generator is (+2) +
This lazy list may return (if requested) the following elements (0, 2, 4, 6, 8, 10)

[source,perl6]
.Lazy list built using a defined generator
----
my $lazylist = (0, { $_ + 3 } ... 12);
say $lazylist;
----
In this example, we defined explicitly a generator enclosed in `{ }` +
This lazy list may return (if requested) the following elements (0, 3, 6, 9, 12)

[WARNING]
====
When using an explicit generator, the endpoint must be one of the values that the generator can return. +
If we reproduce the above example with the endpoint being 10 instead of 12, it will not stop.
The generator _jumps over_ the endpoint.

Alternatively you can replace `0 ... 10` with `0 ...^ * > 10` +
You can read it as: From 0 until the first value greater than 10 (excluding it)

[source,perl6]
.This will not stop the generator
----
my $lazylist = (0, { $_ + 3 } ... 10);
say $lazylist;
----

[source,perl6]
.This will stop the generator
----
my $lazylist = (0, { $_ + 3 } ...^ * > 10);
say $lazylist;
----
====

=== Closures
All code objects in Perl 6 are closures, which means they can reference lexical variables from an outer scope.

[source,perl6]
----
sub generate-greeting {
    my $name = "John Doe";
    sub greeting {
      say "Good Morning $name";
    };
    return &greeting;
}
my $generated = generate-greeting;
$generated();
----

If you run the above code, it will display `Good Morning John Doe` on the terminal. +
While the result is fairly simple, what is interesting about this example, is that the `greeting` inner subroutine was returned from the outer subroutine before being executed.

`$generated` has become a *closure*.

A *closure* is a special kind of object that combines two things:

* A Subroutine

* The Environment in which that subroutine was created.

The environment consists of any local variable that was in-scope at the time that the closure was created.
In this case, `$generated` is a closure that incorporates both the `greeting` subroutine and the `John Doe` string that existed when the closure was created.

Let's take a look at a more interesting example.
[source,perl6]
----
sub greeting-generator($period) {
  return sub ($name) {
    return "Good $period $name"
  }
}
my $morning = greeting-generator("Morning");
my $evening = greeting-generator("Evening");

say $morning("John");
say $evening("Jane");
----
In this example, we have defined a subroutine `greeting-generator($period)` that accepts a single argument `$period`
and returns a new subroutine. The subroutine it returns accepts a single argument `$name` and returns the constructed greeting.

Basically, `greeting-generator` is a subroutine factory. In this example, we used `greeting-generator` to create two new subroutines,
one that says `Good Morning` and one that says `Good Evening`.

`$morning` and `$evening` are both closures. They share the same subroutine body definition, but store different environments. +
In `$morning` 's environment `$period` is `Morning`. In `$evening` 's environment `$period` is `Evening`.

== Classes & Objects
In the previous chapter, we learned how Perl 6 facilitates Functional Programming. +
In this chapter we will take a look at Object Oriented programming in Perl 6.

=== Introduction

_Object Oriented_ programming is one of the widely used paradigms nowadays. +
An *object* is a set of variables and subroutines bundled together. +
The variables are called *attributes* and the subroutines are called *methods*. +
Attributes define the *state* and methods define the *behavior* of an object.

A *class* is a template for creating *objects*. +

In order to understand the relationship consider the below example:

|===

| There are 4 people present in a room | *objects* => 4 people

| These 4 people are humans | *class* => Human

| They have different names, age, sex and nationality | *attributes* => name, age, sex, nationality

|===

In _object oriented_ parlance, we say that objects are *instances* of a class.

Consider the below script:
[source,perl6]
----
class Human {
  has $.name;
  has $.age;
  has $.sex;
  has $.nationality;
}

my $john = Human.new(name => 'John', age => 23, sex => 'M', nationality => 'American');
say $john;
----
The `class` keyword is used to define a class. +
The `has` keyword is used to define attributes of a class. +
The `.new()` method is called a *constructor*. It creates the object as an instance of the class it has been called on.

In the above script, a new variable `$john` holds a reference to a new instance of "Human" defined by `Human.new()`. +
The arguments passed to the `.new()` method are used to set the attributes of the underlying object.

A class can be given _lexical scope_ using `my`:
[source,perl6]
----
my class Human {

}
----

=== Encapsulation
Encapsulation  is an object oriented concept that bundles a set of data and methods together. +
The data (attributes) within an object should be *private*, in other words, accessible only from within the object. +
In order to access the attributes from outside the object, we use methods called *accessors*.

The below two scripts have the same result.

.Direct access to the variable:
[source,perl6]
----
my $var = 7;
say $var;
----

.Encapsulation:
[source,perl6]
----
my $var = 7;
sub sayvar {
  $var;
}
say sayvar;
----
The method `sayvar` is an accessor. It lets us access the value of the variable without getting direct access to it.

Encapsulation is facilitated in Perl 6 with the use of *twigils*. +
Twigils are secondary _sigils_. They come between the sigil and the attribute name. +
Two twigils are used in classes:

* `!` is used to explicitly declare that the attribute is private.
* `.` is used to automatically generate an accessor for the attribute.

By default, all attributes are private but it is a good habit to always use the `!` twigil.

Therefore, we should rewrite the above class as:
[source,perl6]
----
class Human {
  has $!name;
  has $!age;
  has $!sex;
  has $!nationality;
}

my $john = Human.new(name => 'John', age => 23, sex => 'M', nationality => 'American');
say $john;
----
Append to the script the following statement: `say $john.age;` +
It will return this error: `Method 'age' not found for invocant of class 'Human'`
because `$!age` is private and can only be used within the object.
Trying to access it outside the object will return an error.

Now replace `has $!age` with `has $.age` and observe the result of `say $john.age;`

=== Named vs. Positional Parameters
In Perl 6, all classes inherit a default `.new()` constructor. +
It can be used to create objects by providing it with arguments. +
The default constructor can only be provided with *named arguments*. +
In our example above, notice that the arguments supplied to `.new()` are defined by name:

* name => 'John'

* age => 23


What if I do not want to supply the name of each attribute each time I want to create an object? +
Then I need to create another constructor that accepts *positional arguments*.

[source,perl6]
----
class Human {
  has $.name;
  has $.age;
  has $.sex;
  has $.nationality;
  # new constructor that overrides the default one.
  method new ($name,$age,$sex,$nationality) {
    self.bless(:$name,:$age,:$sex,:$nationality);
  }
}

my $john = Human.new('John',23,'M','American');
say $john;
----

=== Methods

==== Introduction
Methods are the _subroutines_ of an object. +
Like subroutines, they are a means of packaging a set of functionality, they accept *arguments*, have a *signature* and can be defined as *multi*.

Methods are defined using the `method` keyword. +
In normal circumstances, methods are required to perform some sort of action on the objects' attributes.
This enforces the concept of encapsulation. Object attributes can only be manipulated from within the object using methods.
The outside world can only interact with the object methods, and has no direct access to its attributes.

[source,perl6]
----
class Human {
  has $.name;
  has $.age;
  has $.sex;
  has $.nationality;
  has $.eligible;
  method assess-eligibility {
      if self.age < 21 {
        $!eligible = 'No'
      } else {
        $!eligible = 'Yes'
      }
  }

}

my $john = Human.new(name => 'John', age => 23, sex => 'M', nationality => 'American');
$john.assess-eligibility;
say $john.eligible;
----

Once methods are defined within a class, they can be called on an object using the _dot notation_: +
_object_ *.* _method_ or as in the above example: `$john.assess-eligibility`

Within the definition of a method, if we need to reference the object itself to call another method we use the `self` keyword. +

Within the definition of a method, if we need to reference an attribute we use `!` even if it was defined with `.` +
The rationale being that what the `.` twigil does is declare an attribute with `!` and automate the creation of an accessor.

In the above example, `if self.age < 21` and `if $!age < 21` would have the same effect, although they are technically different:

* `self.age` calls the `.age` method (accessor) +
Can be written alternatively as `$.age`
* `$!age` is a direct call to the variable

==== Private methods
Normal methods can be called on objects from outside the class.

*Private methods* are methods that can only be called from within the class. +
A possible use case would be a method that calls another one for specific action.
The method that interfaces with the outside world is public while the one referenced should stay private.
We do not want users to call it directly, so we declare it as private.

The declaration of a private method requires the use of the `!` twigil before its name. +
Private methods are called with `!` instead of `.`

[source,perl6]
----
method !iamprivate {
  # code goes in here
}

method iampublic {
  self!iamprivate;
  # do additional things
}
----

=== Class Attributes

*Class attributes* are attributes that belong to the class itself and not to its objects. +
They can be initialized during definition. +
Class attributes are declared using `my` instead of `has`. +
They are called on the class itself instead of its objects.

[source,perl6]
----
class Human {
  has $.name;
  my $.counter = 0;
  method new($name) {
    Human.counter++;
    self.bless(:$name);
  }
}
my $a = Human.new('a');
my $b = Human.new('b');

say Human.counter;
----

=== Access Type
Until now, all the examples that we've seen have used accessors to *get* information from the objects' attributes.

What if we need to modify the value of an attribute? +
We need to label it as _read/write_ using the keywords `is rw`
[source,perl6]
----
class Human {
  has $.name;
  has $.age is rw;
}
my $john = Human.new(name => 'John', age => 21);
say $john.age;

$john.age = 23;
say $john.age;
----
By default, all attributes are declared as _read only_ but you can explicitly do it using `is readonly`

=== Inheritance
==== Introduction
*Inheritance* is another concept of object oriented programming.

When defining classes, soon enough we will realize that some attributes/methods are common to many classes. +
Should we duplicate code? +
NO! We should use *inheritance*

Let's consider we want to define two classes, a class for Human beings and a class for Employees. +
Human beings have 2 attributes: name and age. +
Employees have 4 attributes: name, age, company and salary

One would be tempted to define the classes as:
[source,perl6]
----
class Human {
  has $.name;
  has $.age;
}

class Employee {
  has $.name;
  has $.age;
  has $.company;
  has $.salary;
}
----
While technically correct, the above piece of code is considered conceptually poor.

A better way to write this would be:
[source,perl6]
----
class Human {
  has $.name;
  has $.age;
}

class Employee is Human {
  has $.company;
  has $.salary;
}
----
The `is` keyword defines inheritance. +
In object oriented parlance, we say Employee is a *child* of Human and that Human is a *parent* of Employee.

All child classes inherit the attributes and methods of the parent class, so there is no need to redefine them.

==== Overriding
Classes inherit all attributes and methods from their parent classes. +
There are cases where we need the method in the child class to behave differently than the one inherited. +
To achieve this, we redefine the method in the child class. +
This concept is called *overriding*.

In the below example, the method `introduce-yourself` is inherited by the Employee class.

[source,perl6]
----
class Human {
  has $.name;
  has $.age;
  method introduce-yourself {
    say 'Hi I am a human being, my name is ' ~ self.name;
  }
}

class Employee is Human {
  has $.company;
  has $.salary;
}

my $john = Human.new(name =>'John', age => 23,);
my $jane = Employee.new(name =>'Jane', age => 25, company => 'Acme', salary => 4000);

$john.introduce-yourself;
$jane.introduce-yourself;
----
Overriding works like this:

[source,perl6]
----
class Human {
  has $.name;
  has $.age;
  method introduce-yourself {
    say 'Hi I am a human being, my name is ' ~ self.name;
  }
}

class Employee is Human {
  has $.company;
  has $.salary;
  method introduce-yourself {
    say 'Hi I am a employee, my name is ' ~ self.name ~ ' and I work at: ' ~ self.company;
  }

}

my $john = Human.new(name =>'John',age => 23,);
my $jane = Employee.new(name =>'Jane',age => 25,company => 'Acme',salary => 4000);

$john.introduce-yourself;
$jane.introduce-yourself;
----

Depending of which class the object is, the right method will be called.

==== Submethods
*Submethods* are a type of method that are not inherited by child classes. +
They are only accessible from the class they were declared in. +
They are defined using the `submethod` keyword.

=== Multiple Inheritance
Multiple inheritance is allowed in Perl 6. A class can inherit from multiple other classes.

[source,perl6]
----
class bar-chart {
  has Int @.bar-values;
  method plot {
    say @.bar-values;
  }
}

class line-chart {
  has Int @.line-values;
  method plot {
    say @.line-values;
  }
}

class combo-chart is bar-chart is line-chart {
}

my $actual-sales = bar-chart.new(bar-values => [10,9,11,8,7,10]);
my $forecast-sales = line-chart.new(line-values => [9,8,10,7,6,9]);

my $actual-vs-forecast = combo-chart.new(bar-values => [10,9,11,8,7,10],
                                         line-values => [9,8,10,7,6,9]);
say "Actual sales:";
$actual-sales.plot;
say "Forecast sales:";
$forecast-sales.plot;
say "Actual vs Forecast:";
$actual-vs-forecast.plot;
----

.`Output`
----
Actual sales:
[10 9 11 8 7 10]
Forecast sales:
[9 8 10 7 6 9]
Actual vs Forecast:
[10 9 11 8 7 10]
----

.Explanation
The `combo-chart` class should be able to hold two series, one for the actual values plotted on bars,
and another for forecast values plotted on a line. +
This is why we defined it as a child of `line-chart` and `bar-chart`. +
You should have noticed that calling the method `plot` on the `combo-chart` didn't yield the required result.
Only one series was plotted. +
Why did this happen? +
`combo-chart` inherits from `line-chart` and `bar-chart`, and both of them have a method called `plot`.
When we call that method on `combo-chart` Perl 6 internals will try to resolve the conflict by calling one of the inherited methods.

.Correction
In order to behave correctly, we should have overridden the method `plot` in the `combo-chart`.

[source,perl6]
----
class bar-chart {
  has Int @.bar-values;
  method plot {
    say @.bar-values;
  }
}

class line-chart {
  has Int @.line-values;
  method plot {
    say @.line-values;
  }
}

class combo-chart is bar-chart is line-chart {
  method plot {
    say @.bar-values;
    say @.line-values;
  }
}

my $actual-sales = bar-chart.new(bar-values => [10,9,11,8,7,10]);
my $forecast-sales = line-chart.new(line-values => [9,8,10,7,6,9]);

my $actual-vs-forecast = combo-chart.new(bar-values => [10,9,11,8,7,10],
                                         line-values => [9,8,10,7,6,9]);
say "Actual sales:";
$actual-sales.plot;
say "Forecast sales:";
$forecast-sales.plot;
say "Actual vs Forecast:";
$actual-vs-forecast.plot;
----

.`Output`
----
Actual sales:
[10 9 11 8 7 10]
Forecast sales:
[9 8 10 7 6 9]
Actual vs Forecast:
[10 9 11 8 7 10]
[9 8 10 7 6 9]
----

=== Roles
*Roles* are similar to classes in that they are a collection of attributes and methods.

Roles are declared with the keyword `role`. Classes that wish to implement a role, do so using the `does` keyword.

.Let's rewrite the multiple inheritance example using roles:
[source,perl6]
----
role bar-chart {
  has Int @.bar-values;
  method plot {
    say @.bar-values;
  }
}

role line-chart {
  has Int @.line-values;
  method plot {
    say @.line-values;
  }
}

class combo-chart does bar-chart does line-chart {
  method plot {
    say @.bar-values;
    say @.line-values;
  }
}

my $actual-sales = bar-chart.new(bar-values => [10,9,11,8,7,10]);
my $forecast-sales = line-chart.new(line-values => [9,8,10,7,6,9]);

my $actual-vs-forecast = combo-chart.new(bar-values => [10,9,11,8,7,10],
                                         line-values => [9,8,10,7,6,9]);
say "Actual sales:";
$actual-sales.plot;
say "Forecast sales:";
$forecast-sales.plot;
say "Actual vs Forecast:";
$actual-vs-forecast.plot;
----

Run the above script and you will see that results are the same.

By now you're asking yourself: If roles behave like classes, what's their use? +
To answer your question, modify the first script used to showcase multiple inheritance,
the one where we _forgot_ to override the `plot` method.

[source,perl6]
----
role bar-chart {
  has Int @.bar-values;
  method plot {
    say @.bar-values;
  }
}

role line-chart {
  has Int @.line-values;
  method plot {
    say @.line-values;
  }
}

class combo-chart does bar-chart does line-chart {
}

my $actual-sales = bar-chart.new(bar-values => [10,9,11,8,7,10]);
my $forecast-sales = line-chart.new(line-values => [9,8,10,7,6,9]);

my $actual-vs-forecast = combo-chart.new(bar-values => [10,9,11,8,7,10],
                                         line-values => [9,8,10,7,6,9]);
say "Actual sales:";
$actual-sales.plot;
say "Forecast sales:";
$forecast-sales.plot;
say "Actual vs Forecast:";
$actual-vs-forecast.plot;
----

.`Output`
----
===SORRY!===
Method 'plot' must be resolved by class combo-chart because it exists in multiple roles (line-chart, bar-chart)
----

.Explanation
If multiple roles are applied to the same class and a conflict exists, a compile-time error will be thrown. +
This is a much safer approach than multiple inheritance, where conflicts are not considered errors and are simply resolved at runtime.

Roles will warn you that there's a conflict.

=== Introspection
*Introspection* is the process of getting information about an object, like its type, attributes or methods.

[source,perl6]
----
class Human {
  has Str $.name;
  has Int $.age;
  method introduce-yourself {
    say 'Hi I am a human being, my name is ' ~ self.name;
  }
}

class Employee is Human {
  has Str $.company;
  has Int $.salary;
  method introduce-yourself {
    say 'Hi I am a employee, my name is ' ~ self.name ~ ' and I work at: ' ~ self.company;
  }
}

my $john = Human.new(name =>'John',age => 23,);
my $jane = Employee.new(name =>'Jane',age => 25,company => 'Acme',salary => 4000);

say $john.WHAT;
say $jane.WHAT;
say $john.^attributes;
say $jane.^attributes;
say $john.^methods;
say $jane.^methods;
say $jane.^parents;
if $jane ~~ Human {say 'Jane is a Human'};
----
Introspection is facilitated by:

* `.WHAT` -- returns the class from which the object was created

* `.^attributes` -- returns all the attributes of the object

* `.^methods` -- returns all the methods that can be called on the object

* `.^parents` -- returns the parent classes of the object

* `~~` is called the smart-match operator.
It evaluates to _True_ if the object is created from the class it is being compared against or any of its inheritances.

[NOTA]
--
For more info on Object Oriented Programming in Perl 6, see:

* https://docs.perl6.org/language/classtut
* https://docs.perl6.org/language/objects
--
== Exception Handling

=== Catching Exceptions
*Exceptions* are a special behavior that happens at runtime when something goes wrong. +
We say that exceptions are _thrown_.

Consider the below script that runs correctly:

[source,perl6]
----
my Str $name;
$name = "Joanna";
say "Hello " ~ $name;
say "How are you doing today?"
----

.`Output`
----
Hello Joanna
How are you doing today?
----

Now consider this script that throws an exception:

[source,perl6]
----
my Str $name;
$name = 123;
say "Hello " ~ $name;
say "How are you doing today?"
----

.`Output`
----
Type check failed in assignment to $name; expected Str but got Int
   in block <unit> at exceptions.pl6:2
----

Notice that whenever an error occurs (in this case, assigning a number to a string variable) the program will stop and other lines of code will not be evaluated.

*Exception handling* is the process of _catching_ an exception that has been _thrown_ in order for the script to continue working.

[source,perl6]
----
my Str $name;
try {
  $name = 123;
  say "Hello " ~ $name;
  CATCH {
    default {
      say "Can you tell us your name again, we couldn't find it in the register.";
    }
  }
}
say "How are you doing today?";
----

.`Output`
----
Can you tell us your name again, we couldn't find it in the register.
How are you doing today?
----

Exception handling is done by using a `try-catch` block.

[source,perl6]
----
try {
  # code goes in here
  # if anything goes wrong, the script will enter the below CATCH block
  # if nothing goes wrong, the CATCH block will be ignored
  CATCH {
    default {
      # the code in here will be evaluated only if an exception has been thrown
    }
  }
}
----

The `CATCH` block can be defined the same way a `given` block is defined.
This means we can _catch_ and handle differently many types of exceptions.

[source,perl6]
----
try {
  # code goes in here
  # if anything goes wrong, the script will enter the below CATCH block
  # if nothing goes wrong, the CATCH block will be ignored
  CATCH {
    when X::AdHoc   { # do something if exception of type X::AdHoc is thrown }
    when X::IO      { # do something if exception of type X::IO is thrown }
    when X::OS      { # do something if exception of type X::OS is thrown }
    default         { # do something if exception is thrown and doesn't belong to the above types }
  }
}
----

=== Throwing Exceptions
Perl 6 also allows you to explicitly throw exceptions. +
Two types of exceptions can be thrown:

* ad-hoc exceptions

* typed exceptions

[source,perl6]
.ad-hoc
----
my Int $age = 21;
die "Error !";
----

[source,perl6]
.typed
----
my Int $age = 21;
X::AdHoc.new(payload => 'Error !').throw;
----

Ad-hoc exceptions are thrown using the `die` subroutine, followed by the exception message.

Typed exceptions are objects, hence the use of the `.new()` constructor in the above example. +
All typed exceptions descend from class `X` , below are a few examples: +
`X::AdHoc` is the simplest exception type +
`X::IO` is related to IO errors +
`X::OS` is related to OS errors +
`X::Str::Numeric` related to trying to coerce a string to a number

NOTA: For a complete list of exception types and their associated methods, go to https://docs.perl6.org/type-exceptions.html


== Regular Expressions
A regular expression, or _regex_, is a sequence of characters that is used for pattern matching. +
Think of it as a pattern.

[source,perl6]
----
if 'enlightenment' ~~ m/ light / {
    say "enlightenment contains the word light";
}
----

In this example, the smart match operator `~~` is used to check if a string (enlightenment) contains the word (light). +
"Enlightenment" is matched against the regex `m/ light /`

=== Regex definition

A regular expression can be defined like this:

* `/light/`

* `m/light/`

* `rx/light/`

Unless specified explicitly, white space is ignored; `m/light/` and `m/ light /` are the same.

=== Matching characters
Alphanumeric characters and the underscore `_` are written as is. +
All other characters have to be escaped using a backslash or surrounded by quotes.

[source,perl6]
.Backslash
----
if 'Temperature: 13' ~~ m/ \: / {
    say "The string provided contains a colon :";
}
----

[source,perl6]
.Single quotes
----
if 'Age = 13' ~~ m/ '=' / {
    say "The string provided contains an equal character = ";
}
----

[source,perl6]
.Double quotes
----
if 'name@company.com' ~~ m/ "@" / {
    say "This is a valid email address because it contains an @ character";
}
----

=== Matching categories of characters
Characters can be classified into categories and we can match against them. +
We can also match against the inverse of that category (everything except it):

|===

| *Category* | *Regex* | *Inverse* | *Regex*

| Word character (letter, digit or underscore) | \w | Any character except a word character | \W

| Digit | \d | Any character except a digit | \D

| Whitespace | \s | Any character except a whitespace | \S

| Horizontal whitespace | \h | Any character except a horizontal whitespace | \H

| Vertical whitespace | \v | Any character except a vertical whitespace | \V

| Tab | \t | Any character except a Tab | \T

| New line | \n | Any character except a new line | \N

|===

[source,perl6]
----
if "John123" ~~ / \d / {
  say "This is not a valid name, numbers are not allowed";
} else {
  say "This is a valid name"
}
if "John-Doe" ~~ / \s / {
  say "This string contains whitespace";
} else {
  say "This string doesn't contain whitespace"
}
----

=== Unicode properties
Matching against categories of characters, as seen in the preceding section, is convenient. +
That being said, a more systematic approach would be to use Unicode properties. +
This allows you to match against categories of characters inside and outside of +
the ASCII standard. +
Unicode properties are enclosed in `<: >`

[source,perl6]
----
if "Devanagari Numbers १२३" ~~ / <:N> / {
  say "Contains a number";
} else {
  say "Doesn't contain a number"
}
if "Привет, Иван." ~~ / <:Lu> / {
  say "Contains an uppercase letter";
} else {
  say "Doesn't contain an upper case letter"
}
if "John-Doe" ~~ / <:Pd> / {
  say "Contains a dash";
} else {
  say "Doesn't contain a dash"
}
----

=== Wildcards
Wildcards can also be used in a regex.

The dot `.` means any single character.

[source,perl6]
----
if 'abc' ~~ m/ a.c / {
    say "Match";
}
if 'a2c' ~~ m/ a.c / {
    say "Match";
}
if 'ac' ~~ m/ a.c / {
    say "Match";
} else {
    say "No Match";
}
----

=== Quantifiers
Quantifiers come after a character and are used to specify how many times we are expecting it.

The question mark `?` means zero or one time.

[source,perl6]
----
if 'ac' ~~ m/ a?c / {
    say "Match";
} else {
    say "No Match";
}
if 'c' ~~ m/ a?c / {
    say "Match";
} else {
    say "No Match";
}
----

The star `*` means zero or multiple times.

[source,perl6]
----
if 'az' ~~ m/ a*z / {
    say "Match";
} else {
    say "No Match";
}
if 'aaz' ~~ m/ a*z / {
    say "Match";
} else {
    say "No Match";
}
if 'aaaaaaaaaaz' ~~ m/ a*z / {
    say "Match";
} else {
    say "No Match";
}
if 'z' ~~ m/ a*z / {
    say "Match";
} else {
    say "No Match";
}
----

The `+` means at least one time.

[source,perl6]
----
if 'az' ~~ m/ a+z / {
    say "Match";
} else {
    say "No Match";
}
if 'aaz' ~~ m/ a+z / {
    say "Match";
} else {
    say "No Match";
}
if 'aaaaaaaaaaz' ~~ m/ a+z / {
    say "Match";
} else {
    say "No Match";
}
if 'z' ~~ m/ a+z / {
    say "Match";
} else {
    say "No Match";
}
----

=== Match Results
Whenever the process of matching a string against a regex is successful,
the match result is stored in a special variable `$/`

[source,perl6]
.Script
----
if 'Rakudo is a Perl 6 compiler' ~~ m/:s Perl 6/ {
    say "The match is: " ~ $/;
    say "The string before the match is: " ~ $/.prematch;
    say "The string after the match is: " ~ $/.postmatch;
    say "The matching string starts at position: " ~ $/.from;
    say "The matching string ends at position: " ~ $/.to;
}
----

.Output
----
The match is: Perl 6
The string before the match is: Rakudo is a
The string after the match is: compiler
The matching string starts at position: 12
The matching string ends at position: 18
----

.Explanation
`$/` returns a _Match Object_ (the string that matches the regex) +
The following methods can be called on the _Match Object_: +
`.prematch` returns the string preceding the match. +
`.postmatch` returns the string following the match. +
`.from` returns the starting position of the match. +
`.to` returns the ending position of the match. +

Noticina: By default, whitespace in a regex definition is ignored. +
If we want to match against a regex containing whitespace, we have to do so explicitly. +
The `:s` in the regex `m/:s Perl 6/` forces whitespace to be considered. +
Alternatively, we could have written the regex as `m/ Perl\s6 /` and used `\s` which represents a whitespace. +
If a regex contains more than a single whitespace, using `:s` is a better option than using `\s` for each and every whitespace.

=== Example
Let's check if an email is valid or not. +
For the sake of this example we will assume that a valid email address has this format: +
first name [dot] last name [at] company [dot] (com/org/net)

WARNING: The regex used in this example for email validation is not very accurate. +
Its sole purpose is to demonstrate regex functionality in Perl 6. +
Do not use it as-is in production.

[source,perl6]
.Script
----
my $email = 'john.doe@perl6.org';
my $regex = / <:L>+\.<:L>+\@<:L+:N>+\.<:L>+ /;

if $email ~~ $regex {
  say $/ ~ " is a valid email";
} else {
  say "This is not a valid email";
}
----

.Output
`john.doe@perl6.org is a valid email`

.Explanation
`<:L>` matches a single letter +
`<:L>+` matches one or more letters +
`\.` matches a single [dot] character +
`\@` matches a single [at] character +
`<:L+:N>` matches a letter or a single number +
`<:L+:N>+` matches one or more letters or numbers +

The regex can be decomposed as following:

* *first name* `<:L>+`

* *[dot]* `\.`

* *last name* `<:L>+`

* *[at]* `\@`

* *company name* `<:L+:N>+`

* *[dot]* `\.`

* *com/org/net* `<:L>+`

[source,perl6]
.Alternatively, a regex can be broken down into multiple named regexes
----
my $email = 'john.doe@perl6.org';
my regex many-letters { <:L>+ };
my regex dot { \. };
my regex at { \@ };
my regex many-letters-numbers { <:L+:N>+ };

if $email ~~ / <many-letters> <dot> <many-letters> <at> <many-letters-numbers> <dot> <many-letters> / {
  say $/ ~ " is a valid email";
} else {
  say "This is not a valid email";
}
----

A named regex is defined using the following syntax: `my regex regex-name { regex definition }` +
A named regex can be called using the following syntax: `<regex-name>`

NOTA: For more info on regexes, see https://docs.perl6.org/language/regexes

== Perl 6 Modules
Perl 6 is a general purpose programming language. It can be used to tackle a multitude of tasks including:
text manipulation, graphics, web, databases, network protocols etc.

Reusability is a very important concept whereby programmers don't have to reinvent the wheel each time they want to do a new task.

Perl 6 allows the creation and redistribution of *modules*. Each module is a packaged set of functionality that can be reused once installed.

_Zef_ is a module management tool that comes with Rakudo Star.

To install a specific module, type the below command in your terminal:

`zef install "module name"`

NOTA: The Perl 6 modules directory can be found on: https://modules.perl6.org/

=== Using Modules
MD5 is a cryptographic hash function that produces a 128-bit hash value. +
MD5 has a variety of applications, including the encryption of stored passwords in a database.
When a new user registers, their credentials are not stored as plain text but rather _hashed_.
The rationale behind this is that if the DB gets compromised, the attacker will not be able to know what the passwords are.

Luckily, you don't need to implement the MD5 algorithm yourself; there's a Perl 6 module already implemented.

Let's install it: +
`zef install Digest::MD5`

Now, run the below script:
[source,perl6]
----
use Digest::MD5;
my $password = "password123";
my $hashed-password = Digest::MD5.new.md5_hex($password);

say $hashed-password;
----
In order to run the `md5_hex()` function that creates hashes, we need to load the required module. +
The `use` keyword loads the module for use in the script.

WARNING: In practice MD5 hashing alone is not sufficient, because it is prone to dictionary attacks. +
It should be combined with a salt link:https://en.wikipedia.org/wiki/Salt_(cryptography)[https://en.wikipedia.org/wiki/Salt_(cryptography)].

== Unicode

Unicode is a standard for encoding and representing text for most writing systems in the world. +
UTF-8 is a character encoding capable of encoding all possible characters, or code points, in Unicode.

Characters are defined by a: +
*Grapheme*: Visual representation. +
*Code point*: A number assigned to the character.

=== Using Unicode

.Let's look at how we can output characters using Unicode
[source,perl6]
----
say "a";
say "\x0061";
say "\c[LATIN SMALL LETTER A]";
----
The above 3 lines showcase different ways of building a character:

. Writing the character directly (grapheme)

. Using `\x` and the code point

. Using `\c` and the code point name

.Now lets output a smiley
[source,perl6]
----
say "☺";
say "\x263a";
say "\c[WHITE SMILING FACE]";
----

.Another example combining two code points
[source,perl6]
----
say "á";
say "\x00e1";
say "\x0061\x0301";
say "\c[LATIN SMALL LETTER A WITH ACUTE]";
----

The letter `á` can be written:

* using its unique code point `\x00e1`

* or as a combination of the code points of `a` and acute `\x0061\x0301`

.Some of the methods that can be used:
[source,perl6]
----
say "á".NFC;
say "á".NFD;
say "á".uniname;
----

.`Output`
----
NFC:0x<00e1>
NFD:0x<0061 0301>
LATIN SMALL LETTER A WITH ACUTE
----

`NFC` returns the unique code point. +
`NFD` decomposes the character and return the code point of each part. +
`uniname` returns the code point name.

.Unicode letters can be used as identifiers:
[source,perl6]
----
my $Δ = 1;
$Δ++;
say $Δ;
----

.Unicode can be used to do math:
[source,perl6]
----
my $var = 2 + ⅒;
say $var;
----

== Parallelism, Concurrency and Asynchrony

=== Parallelism
Under normal circumstances, all tasks in a program run sequentially. +
This might not be a problem, unless what you're trying to do takes a lot of time.

Thankfully, Perl 6 has features that will enable you to run things in parallel. +
At this stage, it is important to note that parallelism can mean one of two things:

* *Task Parallelism*: Two (or more) independent expressions running in parallel.

* *Data Parallelism*: A single expression iterating over a list of elements in parallel.

Let's begin with the latter.

==== Data Parallelism
[source,perl6]
----
my @array = (0..50000);                     # Array population
my @result = @array.map({ is-prime $_ });   # call is-prime for each array element
say now - INIT now;                         # Output the time it took for the script to complete
----

.Considering the above example:
We are only doing one operation `@array.map({ is-prime $_ })` +
The `is-prime` subroutine is being called for each array element sequentially: +
`is-prime @array[0]` then `is-prime @array[1]` then `is-prime @array[2]` etc.

.Fortunately we can call `is-prime` on multiple array elements at the same time:
[source,perl6]
----
my @array = (0..50000);                         # Array population
my @result = @array.race.map({ is-prime $_ });  # call is-prime for each array element
say now - INIT now;                             # Output the time it took to complete
----

Notice the use of `race` in the expression.
This method will enable parallel iteration of the array elements.

After running both examples (with and without `race`), compare the time it took for both scripts to complete.

[Noticina]
====
`race` will not preserve the order of elements. If you wish to do, so use `hyper` instead.

[source,perl6]
.race
----
my @array = (1..1000);
my @result = @array.race.map( {$_ + 1} );
.say for @result;
----

[source,perl6]
.hyper
----
my @array = (1..1000);
my @result = @array.hyper.map( {$_ + 1} );
.say for @result;
----

If you run both examples, you should notice that one is sorted and the other is not.

====

==== Task Parallelism

[source,perl6]
----
my @array1 = (0..49999);
my @array2 = (2..50001);

my @result1 = @array1.map( {is-prime($_ + 1)} );
my @result2 = @array2.map( {is-prime($_ - 1)} );

say @result1 eqv @result2;

say now - INIT now;
----

.Considering the above example:

. We defined 2 arrays

. applied a different operation for each array and stored the results

. and checked if both results are the same

The script waits for `@array1.map( {is-prime($_ + 1)} )` to finish +
and then evaluates `@array2.map( {is-prime($_ - 1)} )`

Both operations applied to each array do not depend on each other.

.Why not do both in parallel?
[source,perl6]
----
my @array1 = (0..49999);
my @array2 = (2..50001);

my $promise1 = start @array1.map( {is-prime($_ + 1)} ).eager;
my $promise2 = start @array2.map( {is-prime($_ - 1)} ).eager;

my @result1 = await $promise1;
my @result2 = await $promise2;

say @result1 eqv @result2;

say now - INIT now;
----

.Explanation
The `start` subroutine evaluates the code and returns *an object of type promise* or shortly *a promise*. +
If the code is evaluated correctly, the _promise_ will be *kept*. +
If the code throws an exception, the _promise_ will be *broken*.

The `await` subroutine waits for a *promise*. +
If it's *kept* it will get the returned values. +
If it's *broken* it will get the exception thrown.

Check the time it took each script to complete.

WARNING: Parallelism always adds a threading overhead. If that overhead is not offset by gains in computational speed, the script will seem slower. +
This is why, using `race`, `hyper`, `start` and `await` for fairly simple scripts can actually slow them down.

=== Concurrency and Asynchrony
NOTA: For more info on Concurrency and Asynchronous Programming, see https://docs.perl6.org/language/concurrency

== Native Calling Interface

Perl 6 gives us the ability to use C libraries, using the Native Calling Interface.

`NativeCall` is a standard module that ships with Perl 6 and offers a set of functionality to ease the job
of interfacing Perl 6 and C.

=== Calling a function

Consider the below C code that defines a function called `hellofromc`.
This function prints on the terminal `Hello from C`. It doesn't accept any argument nor return any value.

[source,c]
.ncitest.c
----
#include <stdio.h>

void hellofromc () {
  printf("Hello from C\n");
}
----

Depending on your OS run the following commands to compile the above C code into a library.

.On Linux:
----
gcc -c -fpic ncitest.c
gcc -shared -o libncitest.so ncitest.o
----

.On Windows:
----
gcc -c ncitest.c
gcc -shared -o ncitest.dll ncitest.o
----

Within the same directory where you compiled your C library, create a new Perl 6
file that contains the below code and run it.

[source,perl6]
.ncitest.pl6
----
use NativeCall;

constant LIBPATH = "$*CWD/ncitest";
sub hellofromc() is native(LIBPATH) { * }

hellofromc();
----

.Explanation:
First of all we declared that we will be using the `NativeCall` module. +
Then we created a constant `LIBPATH` that holds the path to the C library. +
Notice that `$*CWD` returns the current working directory. +
Then we created a new Perl 6 subroutine called `hellofromc()` that should act as a
wrapper to its counterpart C function having the same name and residing in the C
library found in `LIBPATH`. +
All of this was done by using the `is native` trait. +
Finally we called our Perl 6 subroutine.

In essence, it all boils down to declaring a subroutine with the trait `is native` and the name of the C library.

=== Renaming a function

In the above part, we saw how we can call a very simple C function by wrapping
it with a Perl 6 subroutine having the same name, using the `is native` trait.

In some cases, we would want to change the name of the Perl 6 subroutine. +
To do so, we use the `is symbol` trait.

Lets modify the above Perl 6 script and rename the Perl 6 subroutine `hello`
instead of `hellofromc`

[source,perl6]
.ncitest.pl6
----
use NativeCall;

constant LIBPATH = "$*CWD/ncitest";
sub hello() is native(LIBPATH) is symbol('hellofromc') { * }

hello();
----

.Explanation:
In case the Perl 6 subroutine has a different name than its C counterpart, we
should use the `is symbol` trait with the name of the original C function.

=== Passing Arguments

Compile the following modified C library and run the Perl 6 script found below again. +
Notice how we modified both C and Perl 6 code to accept a string (`char*` in C and `Str` in Perl 6)

[source,c]
.ncitest.c
----
#include <stdio.h>

void hellofromc (char* name) {
  printf("Hello, %s! This is C!\n", name);
}
----

[source,perl6]
.ncitest.pl6
----
use NativeCall;

constant LIBPATH = "$*CWD/ncitest";
sub hello(Str) is native(LIBPATH) is symbol('hellofromc') { * }

hello('Jane');
----

=== Returning values

Lets repeat the process one more time and create a simple calculator that takes
2 integers and add them. +
Compile the C library and run the Perl 6 script.

[source,c]
.ncitest.c
----
int add (int a, int b) {
  return (a + b);
}
----

[source,perl6]
.ncitest.pl6
----
use NativeCall;

constant LIBPATH = "$*CWD/ncitest";
sub add(int32,int32) returns int32 is native(LIBPATH) { * }

say add(2,3);
----

Notice how both C and Perl 6 functions accept two integers and return one
(`int` in C and `int32` in Perl 6)

=== Types

You might have asked yourself why did we use `int32` instead of `Int` in the latest Perl 6 script. +
Some Perl 6 types like `Int`, `Rat` etc. can't be used as is to pass and receive values from a C function. +
One must use in Perl 6 the same types as the ones in C.

Luckily Perl 6 provides many types that map to their respective C counterpart.

[cols="^.^,^.^",options="header"]
|===

| C Type | Perl 6 Type

| `char` .2+| `int8`

| `int8_t`

| `short` .2+| `int16`

| `int16_t`

| `int` .2+| `int32`

| `int32_t`

| `int64_t` | `int64`

| `unsigned char` .2+| `uint8`

| `uint8_t`

| `unsigned short` .2+| `uint16`

| `uint16_t`

| `unsigned int` .2+| `uint32`

| `uint32_t`

| `uint64_t` | `uint64`

| `long` | `long`

| `long long` | `longlong`

| `float` | `num32`

| `double` | `num64`

| `size_t` | `size_t`

| `bool` | `bool`

| `char*` (String) | `Str`

| Arrays: For example `int*` (Array of int) and `double*` (Array of double) | `CArray`: For example `CArray[int32]` and `CArray[num64]`

|===

NOTA: For more info on the Native Calling Interface, see https://docs.perl6.org/language/nativecall

== The Community

* link:irc://irc.freenode.net/#perl6[#perl6] IRC channel. Much discussion happens on IRC. This should be your go to place for any enquiry: https://perl6.org/community/irc

* link:https://p6weekly.wordpress.com[p6weekly] a weekly overview of changes in and around Perl 6.

* link:http://pl6anet.org[pl6anet] blog aggregator. Stay tuned by reading blog posts that focus on Perl 6.

* link:https://www.reddit.com/r/perl6/[/r/perl6] Subscribe to the Perl 6 subreddit.
